#version 430

precision highp float;

layout(local_size_x = 1, local_size_y = 1) in;

struct Node {
    ivec4 position;
    vec4 color;
    int halfSize;
    int sub;
};

layout(std430, binding=4) readonly buffer Octree_data {
    Node nodes[];
};

struct HitPoint {
    float distance;
    bool hit;
    vec3 color;

    bool debug;
    ivec3 debugPos;
};

layout(rgba32f) uniform image2D img_output;

layout(location=2) uniform vec3 start_position;

layout(location=3) uniform vec2 camera_angles;

layout(location=4) uniform vec2 window_size;

layout(location=5) uniform ivec3 front_voxel;

bool nodeIsLeaf(in Node node) {
    return node.color.a != -1.0f;
}

ivec3 getSubVector(in ivec3 vec, in Node node) {
    ivec3 subVector = ivec3(0, 0, 0);

    const ivec3 pos = node.position.xyz;
    if (vec.x < pos.x) return ivec3(-1, -1, -1);
    if (vec.y < pos.y) return ivec3(-1, -1, -1);
    if (vec.z < pos.z) return ivec3(-1, -1, -1);

    const int halfSize = node.halfSize;
    const int size = halfSize * 2;
    if (vec.x > pos.x + size) return ivec3(-1, -1, -1);
    if (vec.y > pos.y + size) return ivec3(-1, -1, -1);
    if (vec.z > pos.z + size) return ivec3(-1, -1, -1);

    subVector.x = vec.x >= pos.x + halfSize? 1 : 0;
    subVector.y = vec.y >= pos.y + halfSize? 1 : 0;
    subVector.z = vec.z >= pos.z + halfSize? 1 : 0;

    return subVector;
}

int getSubIndex(in ivec3 vec, in Node node) {
    int subIndex = 0;

    const ivec3 pos = node.position.xyz;
    if (vec.x < pos.x) return -1;
    if (vec.y < pos.y) return -2;
    if (vec.z < pos.z) return -3;

    const int halfSize = node.halfSize;
    const int size = halfSize * 2;
    if (vec.x > pos.x + size) return -4;
    if (vec.y > pos.y + size) return -5;
    if (vec.z > pos.z + size) return -6;

    subIndex |= vec.x >= pos.x + halfSize? 2 : 0;
    subIndex |= vec.y >= pos.y + halfSize? 4 : 0;
    subIndex |= vec.z >= pos.z + halfSize? 1 : 0;

    return subIndex;
}

bool findVoxel(in ivec3 voxelPos, inout HitPoint result) {
    int index = 0;
    while(index != -1) {
        const Node currentNode = nodes[index];
        if (currentNode.sub == -1) {
            if (nodeIsLeaf(currentNode)) {
                result.hit = true;
                result.color = currentNode.color.rgb;
                return true;
            }
            return true;
        }

        const int subIndex = getSubIndex(voxelPos, currentNode);
        if (subIndex < 0) return false;

        index = currentNode.sub + subIndex;
    }
    return false;
}

void debugCast(in vec3 rayDirection, in float maxDistance, inout HitPoint result) {
    result.hit = false;
    result.distance = 0.0f;

    const vec3 rayStepSize = abs(1.0f / rayDirection);
    vec3 rayLength;
    ivec3 voxelPos = ivec3(start_position);
    ivec3 step = ivec3(sign(rayDirection));
    int nodeSize = nodes[0].halfSize * 2;
    rayLength = (step * (voxelPos - start_position) + (step * 0.5f) + 0.5f) * rayStepSize;

    const int maxSize = nodeSize;
    bvec3 mask;
    int iter = 0;
    while(iter++ < 200) {
        mask = lessThanEqual(rayLength.xyz, min(rayLength.yzx, rayLength.zxy));
        rayLength += vec3(mask) * rayStepSize;
        voxelPos += ivec3(vec3(mask)) * step;

        if (!result.hit) {
            findVoxel(voxelPos, result);
        }

        if (front_voxel == voxelPos) {
            result.hit = true;
            result.color.g = 1.0f;
            result.color.b = 0;
            result.color.r = 1.0f;
            return;
        }

        if (result.hit) {
            break;
        }
    }
}

int getSubIndexFromSubVector(in const ivec3 vec) {
    int subIndex = 0;

    if (vec.x < 0) return -1;
    if (vec.y < 0) return -2;
    if (vec.z < 0) return -3;

    if (vec.x > 1) return -4;
    if (vec.y > 1) return -5;
    if (vec.z > 1) return -6;

    subIndex |= (vec.x >= 1 ? 2 : 0);
    subIndex |= (vec.y >= 1 ? 4 : 0);
    subIndex |= (vec.z >= 1 ? 1 : 0);

    return subIndex;
}

struct Layer {
    int nodeIndex;
    int halfSize;
    vec3 position;
    ivec3 subVec;
    vec3 rayLength;
    float distance;
};

Layer calculateLayer(inout Layer layer, in const ivec3 dir, in const vec3 rayStepSizeSingle) {
    layer.subVec = min(ivec3(layer.position) / layer.halfSize, 1);
    layer.rayLength = (vec3(layer.subVec + dir) * float(layer.halfSize) - layer.position) * rayStepSizeSingle;
    layer.distance = 0.0f;
    return layer;
}

void moveLayer(inout Layer currentLayer, in const ivec3 step, in const vec3 rayStepSize) {
    const bvec3 mask = lessThanEqual(currentLayer.rayLength.xyz, min(currentLayer.rayLength.yzx, currentLayer.rayLength.zxy));
    currentLayer.distance = length(vec3(mask) * currentLayer.rayLength);
    currentLayer.rayLength += vec3(mask) * rayStepSize;
    currentLayer.subVec += ivec3(vec3(mask)) * step;
}

vec4 castRay(in vec3 rayDirection) {
    const vec3 rayStepSizeSingle = 1.0f / rayDirection;
    const ivec3 step = ivec3(sign(rayDirection));
    const ivec3 dir = max(step, 0);
    const int maxDepth = 6;

    Layer layers[6];
    int currentDepth = maxDepth - 1;
    Layer currentLayer;
    Node currentNode = nodes[0];
    currentLayer.nodeIndex = 0;
    currentLayer.halfSize = currentNode.halfSize;
    currentLayer.position = start_position;
    layers[currentDepth] = calculateLayer(currentLayer, dir, rayStepSizeSingle);

    vec3 rayStepSize = vec3(currentLayer.halfSize) * abs(rayStepSizeSingle);
    float realDistance = 0.0f;
    int iter = 0;
    while (iter++ < 500) {
        currentLayer = layers[currentDepth];
        currentNode = nodes[currentLayer.nodeIndex];
    
        const int subIndex = getSubIndexFromSubVector(currentLayer.subVec);
        if (subIndex < 0) {
            currentDepth++;
            if (currentDepth >= maxDepth) return vec4(0, 0, 0, 0);
            rayStepSize *= 2;
        } else if (currentNode.sub != -1) {
            const Node subNode = nodes[currentNode.sub + subIndex];
            if (subNode.color.a != -1.0f) {
                return subNode.color;
            } else if (currentDepth != 0) { // If currentDepth == 1 this node have voxel neighbour,
                currentDepth--;
                rayStepSize *= 0.5f;

                // Start subRaycast
                Layer newLayer;
                newLayer.nodeIndex = currentNode.sub + subIndex;
                newLayer.halfSize = subNode.halfSize;
                newLayer.position = currentLayer.position + currentLayer.distance * rayDirection - vec3(currentLayer.halfSize * currentLayer.subVec);
                layers[currentDepth] = calculateLayer(newLayer, dir, rayStepSizeSingle);

                realDistance += currentLayer.distance;
                continue;
            }
        }

        moveLayer(layers[currentDepth], step, rayStepSize);
    }
    return vec4(0, 0, 0, 0);
}

vec3 rotate(const in vec3 v, const in vec3 axis, float theta) {
    const float cos_theta = cos(theta);
    return (v * cos_theta) + (cross(axis, v) * sin(theta)) + (axis * dot(axis, v)) * (1 - cos_theta);
}

void main() {
    const ivec2 pixel_coord = ivec2(gl_GlobalInvocationID.xy );
    const ivec3 screen_coord = ivec3(pixel_coord - ivec2(window_size.x / 2, window_size.y / 2), window_size.x);

    vec3 rayDir = normalize(screen_coord);
    rayDir = rotate(rayDir, vec3(1, 0, 0), camera_angles.y);
    rayDir = rotate(rayDir, vec3(0, 1, 0), camera_angles.x);

    imageStore(img_output, pixel_coord, castRay(rayDir));
}
